# Contributor: MaÃ«l <alemswaj+tutanota+com>

_pkgname=hypnotix
pkgname=hypnotix-git
pkgver=2.0.r2.g23f0350
pkgrel=1
pkgdesc="An IPTV streaming application"
arch=(any)
url="https://github.com/linuxmint/hypnotix"
license=(GPL3)
depends=(dconf hicolor-icon-theme python-cairo python-gobject python-imdbpy python-requests python-setproctitle python-unidecode mpv xapp)
optdepends=('youtube-dl: YouTube channel support')
makedepends=(git)
provides=(hypnotix)
conflicts=(hypnotix)
source=(
  "${_pkgname}::git+https://github.com/linuxmint/hypnotix.git"
  force-gdk-backend-x11.patch)
sha256sums=('SKIP'
            'e0ae65141a120365bdb1ff11c14a8884a2e386700a95e4e4d20f7045f1097eda')

pkgver() {
	cd "$_pkgname"
	git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
	sed -i "s/__DEB_VERSION__/$pkgver/g" usr/lib/hypnotix/hypnotix.py
  patch -Np1 -i "$srcdir"/force-gdk-backend-x11.patch
}

build() {
  cd "${srcdir}/${_pkgname}"
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	install -D -t "$pkgdir"/usr/bin usr/bin/hypnotix
	install -D -t "$pkgdir"/usr/lib/hypnotix usr/lib/hypnotix/{common,hypnotix}.py
	install -Dm644 -t "$pkgdir"/usr/lib/hypnotix usr/lib/hypnotix/mpv.py
	install -Dm644 -t "$pkgdir"/usr/share/applications usr/share/applications/hypnotix.desktop
	install -Dm644 -t "$pkgdir"/usr/share/glib-2.0/schemas usr/share/glib-2.0/schemas/org.x.hypnotix.gschema.xml
	install -Dm644 -t "$pkgdir"/usr/share/hypnotix usr/share/hypnotix/*.{css,png,ui}
	install -Dm644 -t "$pkgdir"/usr/share/hypnotix/pictures usr/share/hypnotix/pictures/*.svg
	install -Dm644 -t "$pkgdir"/usr/share/hypnotix/pictures/badges usr/share/hypnotix/pictures/badges/*
	install -Dm644 -t "$pkgdir"/usr/share/icons/hicolor/scalable/apps usr/share/icons/hicolor/scalable/apps/hypnotix.svg

	# Translations
	cp -a usr/share/locale "$pkgdir"/usr/share/locale
}
