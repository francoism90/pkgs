# Maintainer: Francois Menning <f.menning at pm me>
# Contributor: Joonas Henriksson <joonas.henriksson at gmail com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: zach <zach {at} zach-adams {dot} com>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de
# Contributor: Philipp Wolfer <ph.wolfer@gmail.com>

pkgbase=arc-gtk-theme-git
_pkgname=arc-theme
pkgname=('arc-gtk-theme-git' 'arc-solid-gtk-theme-git')
pkgver=20210127.r51.gf896a945
pkgrel=1
pkgdesc="A flat theme with transparent elements for GTK 3, GTK 2 and Gnome-Shell"
arch=('any')
url="https://github.com/jnsh/arc-theme"
license=('GPL3')
makedepends=('meson' 'sassc' 'inkscape' 'git')
optdepends=('arc-icon-theme: recommended icon theme'
            'gtk-engine-murrine: for gtk2 themes'
            'gnome-themes-standard: for gtk2 themes')
source=("${_pkgname}::git+https://github.com/jnsh/${_pkgname}.git")
md5sums=('SKIP')

# Latest stable Arch package versions
_cinnamonver=4.8
_gnomeshellver=40
_gtk3ver=3.24

pkgver() {
  cd "${_pkgname}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${_pkgname}"
  sed -i 's/$font-size: 11;/$font-size: 10;/g' common/gnome-shell/"${_gnomeshellver}"/sass/_common.scss
}

build() {
  cd "${_pkgname}"

  # non-solid themes
  meson --prefix=/usr build \
    -Dcinnamon_version="${_cinnamonver}" \
    -Dgnome_shell_version="${_gnomeshellver}" \
    -Dgtk3_version="${_gtk3ver}"
  meson compile -C build

  # solid-theme
  meson --prefix=/usr build-solid \
    -Dtransparency=false \
    -Dcinnamon_version="${_cinnamonver}" \
    -Dgnome_shell_version="${_gnomeshellver}" \
    -Dgtk3_version="${_gtk3ver}"
  meson compile -C build-solid
}

package_arc-gtk-theme-git() {
  conflicts=('arc-gtk-theme')

  cd "${_pkgname}"
  DESTDIR="$pkgdir" meson install -C build
}

package_arc-solid-gtk-theme-git() {
  pkgdesc="A flat theme suite without transparent elements."
  conflicts=('arc-solid-gtk-theme')

  cd "${_pkgname}"
  DESTDIR="$pkgdir" meson install -C build-solid
}
