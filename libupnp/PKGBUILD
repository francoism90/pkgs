# Maintainer: Francois Menning <f.menning@pm.me>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

pkgname=libupnp
pkgver=1.10.1
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
url='http://pupnp.sourceforge.net/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig')
install=libupnp.install
source=(https://downloads.sourceforge.net/sourceforge/pupnp/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('b97dad43f5fbb1dad34384389ab038823862348e9e5f6d649d845d512165ed92')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-ipv6 \
    --enable-reuseaddr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
