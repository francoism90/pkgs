# Maintainer: Francois Menning <f.menning@pm.me>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: sulaweyo <sledge.sulaweyo@gmail.com>

pkgname=gerbera
pkgver=1.3.1
pkgrel=1
pkgdesc="UPnP Media Server"
arch=(i686 x86_64 armv7h)
url="https://github.com/gerbera/gerbera"
license=('GPL2')
depends=('taglib' 'curl' 'sqlite' 'file' 'ffmpeg' 'ffmpegthumbnailer' 'gcc-libs' 'libupnp>=1.8.4'
         'duktape' 'libexif' 'expat')
makedepends=('cmake')
install=gerbera.install
options=('emptydirs')
source=("$pkgname.tar.gz::https://github.com/gerbera/gerbera/archive/v$pkgver.tar.gz"
        gerbera.sysusers
        gerbera.tmpfiles)
sha256sums=('9c4509189eab6bff0fe183e1dd54ea8cb17d7fb0bc6e21bb5b1a8ccaaf6c60d9'
            '0d44981dd819dad18503d5e116838d085d49a04cc9ca7795f1aaeae1a90a9693'
            '74872bf2d5ae518ef28af183442da3270431e9622a7687957d86657ca8b2cd81')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # config options can be found here: https://github.com/gerbera/gerbera/blob/master/CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_AVCODEC=1 -DWITH_FFMPEGTHUMBNAILER=1 .
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}"/var/lib/gerbera
  install -dm755 "${pkgdir}"/etc/gerbera
  install -Dm644 "${srcdir}/${pkgname}".tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -Dm644 "${srcdir}/${pkgname}".sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}
