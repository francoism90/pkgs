# Maintainer: Francois Menning <f.menning@pm.me>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: sulaweyo <sledge.sulaweyo@gmail.com>

pkgname=gerbera
pkgver=1.3.4
pkgrel=1
pkgdesc="UPnP Media Server"
arch=(i686 x86_64 armv7h)
url="https://github.com/gerbera/gerbera"
license=('GPL2')
depends=('taglib' 'curl' 'sqlite' 'file' 'ffmpeg' 'ffmpegthumbnailer' 'gcc-libs' 'libupnp>=1.8.4'
         'duktape' 'libexif' 'expat')
makedepends=('cmake')
install=gerbera.install
options=('emptydirs')
source=("$pkgname.tar.gz::https://github.com/gerbera/gerbera/archive/v$pkgver.tar.gz"
        0001-fix-config-path.patch
        gerbera.sysusers
        gerbera.tmpfiles)
sha256sums=('c7fbb6104dd03ed22d114c7b1fbe95eb7f90893148630e16b0a4c23f1f631341'
            '8add2597ec123c28957c7fee8237e6068b023915d4ce8ac5345ebb0ce7230913'
            '0d44981dd819dad18503d5e116838d085d49a04cc9ca7795f1aaeae1a90a9693'
            '00221a763d89a411e605d2a1df1c5fb7c44d9dae6b02f861e1ba120ea904eab3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix path to our config
  patch -Np1 < "${srcdir}/0001-fix-config-path.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # config options can be found here: https://github.com/gerbera/gerbera/blob/master/CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_AVCODEC=1 -DWITH_FFMPEGTHUMBNAILER=1 .
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}"/var/lib/gerbera
  install -Dm644 "${srcdir}/${pkgname}".tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -Dm644 "${srcdir}/${pkgname}".sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}
