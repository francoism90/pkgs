# Maintainer: Francois Menning <f.menning@pm.me>

_gitname=mcmojave-kde
pkgname=mcmojave-kde-theme-git
pkgver=r64.78023ef
pkgrel=1
pkgdesc="McMojave theme for the KDE Plasma desktop."
arch=('any')
url="https://github.com/vinceliuice/mcmojave-kde"
license=('GPL3')
depends=('plasma-desktop')
optdepends=('kvantum-qt5' 'mcmojave-gtk-theme-git' 'mcmojave-icon-theme-git' 'sddm')
makedepends=('git')
source=(
  "${_gitname}::git+https://github.com/vinceliuice/mcmojave-kde.git"
  'fix-theme-installer-path.patch'
)
sha256sums=('SKIP'
            '44aacbb3da4f7e048949591473d368c53b3b28e7b1e574a8e3de0f6377b8af8a')

pkgver() {
  cd ${_gitname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare () {
  export SRCDIR="$srcdir/$_gitname"
  export PKGDIR="$pkgdir"

  cd "$SRCDIR"
  patch -Np1 -i "$srcdir"/fix-theme-installer-path.patch
}

package() {
  export SRCDIR="$srcdir/$_gitname"
  export PKGDIR="$pkgdir"

  cd "$SRCDIR"
  bash install.sh

  # sddm
  sddmdir="${pkgdir}/usr/share/sddm/themes"

  install -dm755 "${sddmdir}"
  cp -r sddm/McMojave "${sddmdir}"
}
